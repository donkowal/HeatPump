#include <OneWire.h>
#include <DallasTemperature.h>
#include <LiquidCrystal.h>
#include <SPI.h>
#include <SD.h>


// Data wire is plugged into port 2 on the Arduino
#define ONE_WIRE_BUS 7
#define TEMPERATURE_PRECISION 12

// Setup a oneWire instance to communicate with any OneWire devices (not just Maxim/Dallas temperature ICs)
OneWire oneWire(ONE_WIRE_BUS);

// initialize the library by associating any needed LCD interface pin
// with the arduino pin number it is connected to
const int rs = 0, en = 1, d4 = 2, d5 = 3, d6 = 4, d7 = 5;
LiquidCrystal lcd(rs, en, d4, d5, d6, d7);

// Pass our oneWire reference to Dallas Temperature. 
DallasTemperature sensors(&oneWire);

int numberOfDevices; // Number of temperature devices found

DeviceAddress tempDeviceAddress; // We'll use this variable to store a found device address

const int chipSelect = 6; // CS pin on arduino
const int buforsize = 30;
File file;
float bufor[buforsize][4];
unsigned pomiar = 0;

void setup(void)
{
  // start lcd(columns, rows)
  lcd.begin(16,2);
  // start serial port
  Serial.begin(9600);
  //while(!Serial);   //wait for serial monitor to be open

  for(int i = 0;i < buforsize; i++)
  {
    for(int j = 0; j < 4; j++){
      bufor[i][j] = -128; //filling buffor
    }
  }
  
  Serial.println("Dallas Temperature IC Control Library Demo");

  // Start up the library
  sensors.begin();
  
  // Grab a count of devices on the wire
  numberOfDevices = sensors.getDeviceCount();
  
  // locate devices on the bus
  Serial.print("Locating devices...");
  
  Serial.print("Found ");
  Serial.print(numberOfDevices, DEC);
  Serial.println(" devices.");

  // report parasite power requirements
  Serial.print("Parasite power is: "); 
  if (sensors.isParasitePowerMode()) Serial.println("ON");
  else Serial.println("OFF");
  
  // Loop through each device, print out address
  for(int i=0;i<numberOfDevices; i++)
  {
    // Search the wire for address
    if(sensors.getAddress(tempDeviceAddress, i))
	{
		Serial.print("Found device ");
		Serial.print(i, DEC);
		Serial.print(" with address: ");
		printAddress(tempDeviceAddress);
		Serial.println();
		
		Serial.print("Setting resolution to ");
		Serial.println(TEMPERATURE_PRECISION, DEC);
		
		// set the resolution to TEMPERATURE_PRECISION bit (Each Dallas/Maxim device is capable of several different resolutions)
		sensors.setResolution(tempDeviceAddress, TEMPERATURE_PRECISION);
		
		 Serial.print("Resolution actually set to: ");
		Serial.print(sensors.getResolution(tempDeviceAddress), DEC); 
		Serial.println();
	}else{
		Serial.print("Found ghost device at ");
		Serial.print(i, DEC);
		Serial.print(" but could not detect address. Check power and cabling");
	}
  }
if(!SD.begin(chipSelect)){
  lcd.print("Brak Karty SD");
  }
else{
      lcd.print("Wczytano Karte");

      String name = "0.txt";
      int fileID = 0;
      
      while(SD.exists(name)){
          fileID++;
          name = String(String(fileID) + ".txt");
        }
      
        file = SD.open(name, FILE_WRITE);
        file.print("T1;T2;T3;T4");
        file.println();
      }
  delay(5000);
  lcd.clear();
}

// function to print the temperature for a device
void printTemperature(DeviceAddress deviceAddress, int i)
{
  int x = 0;
  int y = 0;

  float tempC = sensors.getTempC(deviceAddress);
  Serial.print("T");
  Serial.print(i);
  Serial.print(": ");
  Serial.print(tempC);
  Serial.print("  ");

  bufor[pomiar][i] = tempC;
  
  switch(i){
    case 0:
      x = 0;
      y = 0;
      break;
    case 1:
      x = 9;
      y = 0;
      break;
    case 2:
      x = 0;
      y = 1;
      break;
    case 3:
      x = 9;
      y = 1;
      break;
  }
  lcd.setCursor(x, y);
  lcd.print("T");
  lcd.print(i);
  lcd.print("=");
  lcd.print(tempC,1);
}

void loop(void)
{ 
  // call sensors.requestTemperatures() to issue a global temperature 
  // request to all devices on the bus
  Serial.print("Requesting temperatures...");
  sensors.requestTemperatures(); // Send the command to get temperatures
  Serial.println("DONE");
  
  // Loop through each device, print out temperature data
  for(int i=0;i<numberOfDevices; i++)
  {
    // Search the wire for address
    if(sensors.getAddress(tempDeviceAddress, i))
	{
		
		// It responds almost immediately. Let's print out the data
		printTemperature(tempDeviceAddress, i);// Use a simple function to print out the data
	}
	//else ghost device! Check your power requirements and cabling
  }
  Serial.println();
  if(pomiar == buforsize-1){
    saveSD();
    pomiar = 0;
  }
  else
    pomiar++;
}

void saveSD()
{
  Serial.println();
  Serial.println("Zapisywanie na kartÄ™");
  Serial.println();
  for(int i = 0; i < buforsize; i++){
      for(int j = 0; j < 4; j++){
        file.print(bufor[i][j]);
        file.print(";");
      }
      file.println();
  }
  file.flush();
}

// function to print a device address
void printAddress(DeviceAddress deviceAddress)
{
  for (uint8_t i = 0; i < 8; i++)
  {
    if (deviceAddress[i] < 16) Serial.print("0");
    Serial.print(deviceAddress[i], HEX);
  }
}
